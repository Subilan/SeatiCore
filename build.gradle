plugins {
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'net.minecraftforge.gradle' version '[6.0,6.2)'
    id 'java-library'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

version = mod_version
group = mod_group_id

base {
    archivesName = mod_id
}

// 1.18+ Java 17
java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"
minecraft {
    mappings channel: mapping_channel, version: mapping_version

    copyIdeResources = true

    runs {
        configureEach {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'

            property 'forge.logging.console.level', 'debug'

            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }
        }

        client {
            property 'forge.enabledGameTestNamespaces', mod_id
        }

        server {
            property 'forge.enabledGameTestNamespaces', mod_id
            args '--nogui'
        }

        gameTestServer {
            property 'forge.enabledGameTestNamespaces', mod_id
        }

        data {
            workingDirectory project.file('run-data')
            args '--mod', mod_id, '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven {
        url "https://oss.sonatype.org/content/repositories/releases/"
    }

    maven {
        url 'https://github.com/CarmJos/EasySQL/blob/repo/'
    }
}

configurations {
    shade
    implementation.extendsFrom shade
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    shade "org.bspfsystems:yamlconfiguration:2.0.2"
    shade group: 'com.mysql', name: 'mysql-connector-j', version: '8.2.0'
    shade "cc.carm.lib:easysql-hikaricp:0.4.7"
    shade "cc.carm.lib:easysql-api:0.4.7"
    shade 'org.java-websocket:Java-WebSocket:1.5.7'
}

tasks.named('processResources', ProcessResources).configure {
    var replaceProperties = [
            minecraft_version   : minecraft_version, minecraft_version_range: minecraft_version_range,
            forge_version       : forge_version, forge_version_range: forge_version_range,
            loader_version_range: loader_version_range,
            mod_id              : mod_id, mod_name: mod_name, mod_license: mod_license, mod_version: mod_version,
            mod_authors         : mod_authors, mod_description: mod_description,
    ]
    inputs.properties replaceProperties

    filesMatching(['META-INF/mods.toml', 'pack.mcmeta']) {
        expand replaceProperties + [project: project]
    }
}

tasks.named('jar', Jar).configure {
    archiveClassifier.set('slim')
    manifest {
        attributes([
                'Specification-Title'     : mod_id,
                'Specification-Vendor'    : mod_authors,
                'Specification-Version'   : '1',
                'Implementation-Title'    : project.name,
                'Implementation-Version'  : project.jar.archiveVersion,
                'Implementation-Vendor'   : mod_authors,
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }

    finalizedBy 'reobfJar'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

shadowJar {
    archiveClassifier.set('')
    configurations = [project.configurations.shade]

    finalizedBy 'reobfShadowJar'
    // Important Note 2024.07.03
    // To prevent ResolutionException at runtime, this must be removed from the package.
    // Since org.slf4j is widely used across many dependencies, Minecraft and Forge, it's easily to see ResolutionException
    // or other kind of conflicts if there are duplicate org.slf4j package present in module.
    //
    // It may be a forge bug, though.
    //
    // This problem is HORRIBLE. See these examples:
    // https://forums.minecraftforge.net/topic/126392-mc-1201forge-4720-crash-on-startup/
    // https://forums.minecraftforge.net/topic/126508-12014721questions-for-modules-sdrp-and-orgslf4j-export-package-orgslf4jspi-to-module/
    // There's not a single reply made so far. What a tragedy!
    //
    // -- Subilan
    exclude 'org/slf4j/**'
}

assemble.dependsOn shadowJar

reobf {
    shadowJar {}
}
